/**
 * Copyright Â© 2017 Thomas Biesaart (thomas.biesaart@gmail.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
use Collection, Date;
include io.chapp.testit.report.MarkdownReport as markdown;
include io.chapp.testit.report.JsonReport as json;
include io.chapp.testit.report.ConsoleReport as console;

function generateReports(suites) {
    var report = {
        "suites": sortSuites(suites),
        "suiteCount": Collection.length(suites),
        "executionDate": Date.now(),
    } + getCaseCounts(suites);
    
    json->generate(report);
    markdown->generate(report);
    console->generate(report);
}

private function sortSuites(suites) {
    var result = {
        "failed": [],
        "successful": [],
        "all": suites
    };
    
    foreach(suite in suites) {
        var failed = [];
        var ok = [];
        foreach(case in suite.cases) {
            if(case.ok) {
                ok[] = case;
            } else {
                failed[] = case;
            }
        }
    
        if(ok != []) {
            result.successful[] = {
                "robot": suite.robot,
                "cases": ok
            };
        }
        
        if(failed != []) {
            result.failed[] = {
                "robot": suite.robot,
                "cases": failed
            };
        }
    }
    
    return result;
}

private function getCaseCounts(suites) {
    var result = {
        "executedTestCount": 0,
        "successfulTestCount": 0,
        "failedTestCount": 0,
    };
    
    foreach(suite in suites) {
        foreach(case in suite.cases) {
            result.executedTestCount++;
            
            if(case.ok) {
                result.successfulTestCount++;
            } else {
                result.failedTestCount++;
            }
        }
    }
    return result;
}